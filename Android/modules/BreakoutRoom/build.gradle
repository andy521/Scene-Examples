if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

def safeExtGetVersion(prop, key, fallback) {
    if (rootProject.ext.has(prop)) {
        def map = rootProject.ext.get(prop)
        if (map.containsKey(key)) {
            return map.get(key)
        }
    }
    return fallback
}

def safeExtGetDependence(key, fallback){
    return "$key:${safeExtGetVersion('dependencies', key, fallback)}"
}


android {
    compileSdkVersion safeExtGetVersion('android', 'compileSdkVersion', 30)
    buildToolsVersion safeExtGetVersion('android', 'buildToolsVersion', '30.0.3')

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "io.agora.sample.breakoutroom"
        }

        minSdkVersion safeExtGetVersion('android', 'minSdkVersion', 21)
        targetSdkVersion safeExtGetVersion('android', 'targetSdkVersion', 30)
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
//            zipAlignEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures{
        viewBinding true
    }
}

dependencies {
    testImplementation safeExtGetDependence('junit:junit','4.13.2')
    androidTestImplementation safeExtGetDependence('androidx.test.ext:junit','1.1.3')
    androidTestImplementation safeExtGetDependence('androidx.test.espresso:espresso-core','3.4.0')

    implementation project(path: ':libs:base-library')
    implementation project(path: ':libs:data-syncmanager')

    implementation safeExtGetDependence('io.agora.rtc:full-sdk', '3.5.1')
    implementation safeExtGetDependence('com.google.code.gson:gson','2.8.9')

    implementation safeExtGetDependence('androidx.navigation:navigation-fragment','2.3.5')
    implementation safeExtGetDependence('androidx.swiperefreshlayout:swiperefreshlayout','1.1.0')
    implementation safeExtGetDependence('androidx.cardview:cardview', '1.0.0')
    implementation safeExtGetDependence('androidx.constraintlayout:constraintlayout', '2.0.4')
    implementation safeExtGetDependence('androidx.fragment:fragment', '1.4.0')
    implementation safeExtGetDependence('com.google.android.material:material', '1.3.0')
}